;dos header
	.word $ffff
	.word start
	.word end-1
	*=$a800

;tape header
;	.word $0400
;	.word $700
;	.word start
;	*= $706
;init:
;	clc
;	rts

start:
	LDX #0
	LDA #$0B
	STA $342
	LDA #<FILESTR
	STA $344
	LDA #>FILESTR
	STA $345
	LDA #<FILESLEN
	STA $348
	LDA #>FILESLEN
	STA $349
	JSR $E456

	LDX #0
	LDA #$05
	STA $342
	LDA #<FINDBUF
	STA $344
	LDA #>FINDBUF
	STA $345
	LDA #8
	STA $348
	LDA #0
	STA $349
	JSR $E456

	LDX $348	;CLEAR
	LDA #0
CLOOP	STA FINDBUF-1,X
	INX
	CPX #11
	BNE CLOOP

	JSR PREP
	LDA #$FE	;CMD CD /
	STA $302
	INC $303
	JSR DOSIO

	LDA #0
	STA FIRST
	STA DIRLEVEL

MAIN	JSR PREP
	LDA #$EC	;CMD FIND
	STA $302
	LDA #$80	;WRITEFLAG
	STA $303
	LDA #<FINDBUF
	STA $304
	LDA #>FINDBUF
	STA $305
	LDA #11	;LENGTH
	STA $308
	JSR DOSIO

GETRES	JSR PREP
	LDA #$ED	;CMD GET RESULTS
	STA $302
	LDA #$40	;READFLAG
	STA $303
	LDA #<FILEBUF	;ADDR
	STA $304
	LDA #>FILEBUF
	STA $305
	LDA #14	;LENGTH
	STA $308
	LDA FINDIDX	;FILEINDEX
	STA $30A
	LDA FINDIDX+1
	STA $30B
	JSR DOSIO

	LDA FILEBUF
	BEQ NEXT

	JSR PUT
	CLC
	LDY FIDX
	INY
	STY FINDIDX
	LDA FIDX+1
	ADC #0
	STA FINDIDX+1
	JMP GETRES

NEXT	LDA #0	;RESET IDX
	STA FINDIDX
	STA FINDIDX+1

	JSR PREP
	LDA #$E4	;GET FILENAME
	STA $302
	LDA #$40
	STA $303
	LDA #<FILEBUF
	STA $304
	LDA #>FILEBUF
	STA $305
	LDA #12	;LENGTH
	STA $308

	LDY FIRST
	BNE S2	
	INC FIRST
	BPL SKIP

S2	LDA #0	;FOR ADC
	INC FIDX	;NEXT ENTRY
	ADC FIDX+1 ;CARRY?

SKIP	LDA FIDX
	STA $30A
	LDA FIDX+1
	STA $30B
	JSR DOSIO

	LDA FFLAGS
	BEQ NODIR
	CMP #$10	;DIR?
	BNE NEXT

	LDA DIRLEVEL
	CMP #10	;MAX DEPTH 10
	BEQ CLOSE
	ASL
	TAX
	LDA FIDX
	STA DIRIDX,X
	LDA FIDX+1
	STA DIRIDX+1,X
	INC DIRLEVEL
	JSR CD
	JMP MAIN

CD	JSR PREP
	LDA #$FF	;CMD CD
	STA $302
	INC $303
	LDA FIDX
	STA $30A
	LDA FIDX+1
	STA $30B
	JSR DOSIO
CLOSE	RTS

NODIR	DEC DIRLEVEL
	BMI CLOSE
	LDA #0
	STA FIDX
	STA FIDX+1
	JSR CD
	LDA DIRLEVEL
	ASL
	TAX
	LDA DIRIDX,X
	STA FIDX
	LDA DIRIDX+1,X
	STA FIDX+1
	JMP NEXT

DOSIO	JSR $E459
	BMI ERR
	RTS

ERR	LDA #$10
	STA 710
	PLA
	PLA
	RTS

PUT	LDX #0
	LDY #0
	LDA $304,Y
	STA $344,X
	LDA $305,Y
	STA $345,X
	LDA #11
	STA $348,X
	LDA #0
	STA $349,X
;	LDA #$B	;PUT BLOCK
	LDA #$9	;PUT TEXT(ADD CR)
	STA $342,X
	JMP $E456

PREP	LDX #16
	LDA #0
PLOOP	STA $300,X	;CLEAR DCB
	DEX
	BNE PLOOP

	LDA #$71	;DEVID
	STA $300
	INC $301	;UNIT 1
	LDA #3	;TIMEOUT
	STA $306
	RTS

FIRST	.byte 0
FILEBUF	.asc "FILENAMEEXT"
FFLAGS	.byte 0
FIDX	.word 0

DIRLEVEL	.byte 0
DIRIDX	.word 0,0,0,0,0,0,0,0,0,0

FINDIDX	.word 0
FINDBUF	.byte 0,0,0,0,0,0,0,0,0,0,0
FILESTR	.asc "FILENAME:"
FILESLEN	= *-FILESTR
end
